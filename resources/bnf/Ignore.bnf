{
  parserClass               = "mobi.hsz.idea.gitignore.parser.IgnoreParser"
  parserUtilClass           = "mobi.hsz.idea.gitignore.parser.IgnoreParserUtil"

  extends                   = "mobi.hsz.idea.gitignore.psi.IgnoreElementImpl"
 
  psiClassPrefix            = "Ignore"
  psiImplClassSuffix        = "Impl"
  psiPackage                = "mobi.hsz.idea.gitignore.psi"
  psiImplPackage            = "mobi.hsz.idea.gitignore.psi.impl"

  elementTypeHolderClass    = "mobi.hsz.idea.gitignore.psi.IgnoreTypes"
  elementTypeClass          = "mobi.hsz.idea.gitignore.psi.IgnoreElementType"
  tokenTypeClass            = "mobi.hsz.idea.gitignore.psi.IgnoreTokenType"
 
  tokens = [
    CRLF                    = "regexp:[\s\r\n]+"
    HEADER                  = "regexp:###.*"
    SECTION                 = "regexp:##.*"
    COMMENT                 = "regexp:#.*"
    BRACKET_LEFT            = "regexp:(?!\\\[)\["
    BRACKET_RIGHT           = "regexp:(?!\\\])\]"
    SLASH                   = "/"
    SYNTAX_KEY              = "syntax:"
    VALUE                   = "regexp:(?![!#\s])(?![\[\]])(?:(?:\\[\[\]])|(?:\/\/)|[^\[\]\/\s])+"
  ]

  name("ENTRY.*")           = "entry"
  mixin("ENTRY")            = "mobi.hsz.idea.gitignore.psi.impl.IgnoreEntryExtImpl"
  implements("ENTRY")       = "mobi.hsz.idea.gitignore.psi.IgnoreEntryBase"
}
 
gitignoreFile           ::= item_ *
private item_           ::= HEADER | SECTION | COMMENT | SYNTAX | ENTRY_DIRECTORY | ENTRY_FILE | CRLF
NEGATION                ::= "!"
SYNTAX                  ::= SYNTAX_KEY CRLF * VALUE

ENTRY_FILE              ::= NEGATION ? SLASH ? <<list_macro value_>>        { extends = "ENTRY" }
ENTRY_DIRECTORY         ::= NEGATION ? SLASH ? <<list_macro value_>> SLASH  { extends = "ENTRY_FILE" }
ENTRY                   ::= NEGATION ? SLASH ? <<list_macro value_>>

private meta bvalue_    ::= BRACKET_LEFT ( VALUE SLASH ? ) + BRACKET_RIGHT  { pin = "BRACKET_LEFT" }
private meta value_     ::= bvalue_ | VALUE
private meta list_macro ::= <<p>> + (SLASH <<p>> +) *
