{
  parserClass               = "mobi.hsz.idea.gitignore.parser.GitignoreParser"

  extends                   = "mobi.hsz.idea.gitignore.psi.GitignoreElementImpl"
 
  psiClassPrefix            = "Gitignore"
  psiImplClassSuffix        = "Impl"
  psiPackage                = "mobi.hsz.idea.gitignore.psi"
  psiImplPackage            = "mobi.hsz.idea.gitignore.psi.impl"

  elementTypeHolderClass    = "mobi.hsz.idea.gitignore.psi.GitignoreTypes"
  elementTypeClass          = "mobi.hsz.idea.gitignore.psi.GitignoreElementType"
  tokenTypeClass            = "mobi.hsz.idea.gitignore.psi.GitignoreTokenType"
 
  tokens = [
    CRLF                    = "regexp:[\s\r\n]+"
    HEADER                  = "regexp:###.*"
    SECTION                 = "regexp:##.*"
    COMMENT                 = "regexp:#.*"
    BRACKET_LEFT            = "regexp:(?!\\\[)\["
    BRACKET_RIGHT           = "regexp:(?!\\\])\]"
    SLASH                   = "/"
    VALUE                   = "regexp:(?![!#\s])(?![\[\]])(?:(?:\\[\[\]])|(?:\/\/)|[^\[\]\/\s])+"
  ]

  mixin("ENTRY")            = "mobi.hsz.idea.gitignore.psi.impl.GitignoreEntryExtImpl"
}
 
gitignoreFile           ::= item_ *
private item_           ::= HEADER | SECTION | COMMENT | ENTRY_DIRECTORY | ENTRY_FILE | CRLF
NEGATION                ::= "!"
ENTRY_FILE              ::= NEGATION ? SLASH ? <<list_macro value_>> { extends="ENTRY" }
ENTRY_DIRECTORY         ::= NEGATION ? SLASH ? <<list_macro value_>> SLASH { extends="ENTRY_FILE" }
ENTRY                   ::= NEGATION ? SLASH ? <<list_macro value_>>

private meta bvalue_    ::= BRACKET_LEFT VALUE BRACKET_RIGHT { pin="BRACKET_LEFT" }
private meta value_     ::= bvalue_ | VALUE
private meta list_macro ::= <<p>> + (SLASH <<p>> +) *
